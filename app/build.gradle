apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

apply from: '../gradle_scripts/apk_signing_configs.gradle'
apply plugin: 'com.google.gms.google-services'

// Hilt. Allow references to generated code
kapt {
    correctErrorTypes true
}

android {
    compileSdk 31

    def apikeyPropertiesFile = rootProject.file("apikey.properties")
    def apikeyProperties = new Properties()
    apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))
    defaultConfig {
        applicationId "ru.gaket.themoviedb"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.1.1"

        buildConfigField("String", "THE_MOVIE_DB_API_KEY", apikeyProperties['THE_MOVIE_DB_API_KEY'])

//        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // using HiltTestRunner for injecting AuthInteractor in test and logout befor elogin
        testInstrumentationRunner = "ru.gaket.themoviedb.tests.HiltTestRunner"
    }

    buildTypes {
        debug {
            signingConfig = signingConfigs.dev
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig = signingConfigs.dev
        }
    }

    flavorDimensions "serverType"
    productFlavors {
        prod {
            dimension "serverType"
            buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
        }
        predprod {
            dimension "serverType"
            // Sample case. Const with the same name can have different value in another BuildVariant
            buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
        }
    }

    variantFilter { variant ->
        def buildType = variant.buildType.name
        def flavor = variant.flavors*.name
        if (buildType == "release" && flavor.contains("predprod")) {
            setIgnore(true)
        }
    }

    buildFeatures {
        viewBinding true
    }

    kapt {
        arguments {
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
            arg("room.schemaLocation", "$projectDir/room_db_schemas".toString())
        }
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
    }
}

ext {
    versions = [
            glide           : "4.6.1",
            gson            : "2.8.7",
            kotlinExt       : "2.3.1",
            kotlinCoroutines: "1.5.2",
            retrofit        : "2.9.0",
            room            : "2.4.0-alpha04",
    ]
}

dependencies {
    implementation "androidx.activity:activity-ktx:1.3.1"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.collection:collection-ktx:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.kotlinExt"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.kotlinExt"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.kotlinExt"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-rc01"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.0-beta01"
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.google.android.material:material:1.4.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "jp.wasabeef:picasso-transformations:2.2.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinCoroutines"

    implementation platform("com.google.firebase:firebase-bom:28.4.1")
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-firestore-ktx"

    androidTestImplementation "com.kaspersky.android-components:kaspresso:1.3.0"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
}
